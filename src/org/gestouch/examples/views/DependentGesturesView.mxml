<?xml version="1.0" encoding="utf-8"?>
<views:ExampleViewBase xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:s="library://ns.adobe.com/flex/spark"
	xmlns:mx="library://ns.adobe.com/flex/mx"
	xmlns:views="org.gestouch.examples.views.*"
	xmlns:controls="org.gestouch.examples.controls.*"
	xmlns:containers="org.gestouch.examples.containers.*"
	>
	
	<fx:Script>
		<![CDATA[
			import com.greensock.TweenMax;
			import com.greensock.plugins.GlowFilterPlugin;
			import com.greensock.plugins.TweenPlugin;

			import org.gestouch.events.TapGestureEvent;
			import org.gestouch.gestures.TapGesture;
			
			
			private var singleTapGesture:TapGesture;
			private var doubleTapGesture:TapGesture;


			override protected function init():void
			{
				super.init();
				
				TweenPlugin.activate([GlowFilterPlugin]);				
			}
			
			
			override protected function onViewActivate():void
			{
				super.onViewActivate();
				
				singleTapGesture = new TapGesture(button);
				singleTapGesture.addEventListener(TapGestureEvent.GESTURE_TAP, onGesture);
				
				doubleTapGesture = new TapGesture(button);
				doubleTapGesture.addEventListener(TapGestureEvent.GESTURE_TAP, onGesture);
				doubleTapGesture.numTapsRequired = 2;
				
				singleTapGesture.requireGestureToFail(doubleTapGesture);
			}
			
			
			override protected function onViewDeactivate():void
			{
				super.onViewDeactivate();
				
				if (singleTapGesture)
				{
					singleTapGesture.removeEventListener(TapGestureEvent.GESTURE_TAP, onGesture);
					singleTapGesture.dispose();
					singleTapGesture = null;
				}
				if (doubleTapGesture)
				{
					doubleTapGesture.removeEventListener(TapGestureEvent.GESTURE_TAP, onGesture);
					doubleTapGesture.dispose();
					doubleTapGesture = null;
				}
			}
			

			private function onGesture(event:TapGestureEvent):void
			{
				var tapGesture:TapGesture = event.target as TapGesture;
				if (tapGesture == singleTapGesture)
				{
					label.text = "Signle-tap recognized.";
				}
				else
				{
					label.text = "Double-tap recognized.";
				}
				
				TweenMax.fromTo(label, 4,
					{autoAlpha: 1},
					{autoAlpha: 0}
				);
				
				TweenMax.fromTo(button, 1,
					{glowFilter: {color: 0xCCCCCC * Math.random(), blurX: 64, blurY: 64, strength: 3, alpha: 1, quality: 1}},
					{glowFilter: {alpha: 0, remove: true}}
				);
			}
		]]>
	</fx:Script>
	
	<s:Label id="label" />
	<s:Button id="button" label="Tap or Double-Tap Me" minWidth="250" minHeight="200" horizontalCenter="0" verticalCenter="0"/>
	
</views:ExampleViewBase>