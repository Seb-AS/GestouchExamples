<?xml version="1.0" encoding="utf-8"?>
<views:ExampleViewBase xmlns:fx="http://ns.adobe.com/mxml/2009"
        xmlns:s="library://ns.adobe.com/flex/spark"
		xmlns:mx="library://ns.adobe.com/flex/mx"
		xmlns:views="org.gestouch.examples.views.*"
		xmlns:controls="org.gestouch.examples.controls.*"
		title="Hold-n-Drag Example"
		>

	<views:layout>
		<s:VerticalLayout/>
	</views:layout>
	
	<fx:Script>
		<![CDATA[
			import com.greensock.TweenMax;
			import com.greensock.plugins.GlowFilterPlugin;
			import com.greensock.plugins.TweenPlugin;

			import org.gestouch.events.DragGestureEvent;
			import org.gestouch.events.LongPressGestureEvent;
			import org.gestouch.gestures.DragGesture;
			import org.gestouch.gestures.LongPressGesture;

			import flash.events.GesturePhase;


			private var longPressGesture:LongPressGesture;
			private var dragGesture:DragGesture;

			
			
			override protected function init():void
			{
				TweenPlugin.activate([GlowFilterPlugin]);
				
				image.addEventListener(LongPressGestureEvent.GESTURE_LONG_PRESS, image_gestureLongPressHandler);	
				image.addEventListener(DragGestureEvent.GESTURE_DRAG, image_gestureDragHandler);
			}
			
			
			override protected function onViewActivate():void
			{
				super.onViewActivate();
				
				longPressGesture = LongPressGesture.add(image);
			}
			
			
			override protected function onViewDeactivate():void
			{
				super.onViewDeactivate();
				
				if (longPressGesture)
				{
					longPressGesture.dispose();
					longPressGesture = null;
				}
			}


			private function image_gestureLongPressHandler(event:LongPressGestureEvent):void
			{
				if (event.phase == GesturePhase.BEGIN)
				{
					image.alpha = 0.5;
					dragGesture = DragGesture.add(image, {slop: 0});
					// start DragGesture at once
					// it's being populated with TouchPoints from another gesture (longPressGesture)
					dragGesture.pickAndContinue(longPressGesture);
					
					TweenMax.to(image, 0.5, {glowFilter: {color: 0xEEEEEE * Math.random(), blurX: 16, blurY: 16, strength: 2, alpha: 1}});
				}
				else if (event.phase == GesturePhase.END)
				{
					image.alpha = 1;
					if (dragGesture)
					{
						dragGesture.dispose();
						dragGesture = null;
						// not that dragGesture won't process touchEnd
						// and won't dispatch event with GesturePhase.END
						// since this handler will is executed earlier
						// (as long press gesture was added earlier and has higher "priority" in a queue)
					}
					
					TweenMax.to(image, 1, {glowFilter: {alpha: 0, remove: true}});
				}
			}


			private function image_gestureDragHandler(event:DragGestureEvent):void
			{
				image.move(image.x + event.offsetX, image.y + event.offsetY);
			}
		]]>
	</fx:Script>
	
	<controls:HintText text="Hold the image and then drag around the screen"/>		
	<s:Image id="image"
		x="60" y="60"
		source="@Embed(source='/../build/assets/images/placekitten.jpg')"
		/>
</views:ExampleViewBase>